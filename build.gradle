import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id "com.gradle.build-scan" version "1.9"
    id "com.matthewprenger.cursegradle" version "1.0.9"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.53"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'groovy'

//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


// version = "0.1.1"
group = "atm.bloodworkxgaming.craftgroovy" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${archivebase}"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

apply from: "gradle/scripts/getbuildnumber.gradle"

sourceSets {
    main {
        groovy { srcDirs = ["src/main/java"] }  // compile   everything in src/ with groovy
        java { srcDirs = [] }    // no source dirs for the java compiler
    }
}

minecraft {
    version = "1.12.2-14.23.2.2623"
    runDir = "run"
    replace "@MAJOR@", version_major
    replace "@MINOR@", version_minor
    replace "@REVIS@", version_revis
    replace "@BUILD@", project.getBuildNumber()

    mappings = "snapshot_20180223"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

apply from: "gradle/scripts/version.gradle"

repositories {
    maven { // crt
        url "http://maven.blamejared.com"
    }

    maven { // jei
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    deobfCompile ('CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.6.457') {
        exclude group: 'org.ow2.asm'
    }
    deobfCompile 'com.teamacronymcoders:ContentTweaker:1.12.2-4.5.0'
    deobfCompile 'com.teamacronymcoders.base:base:1.12.2-3.7.0-SNAPSHOT.132'
    deobfCompile "mezz.jei:jei_1.12.2:4.8.5.159"
    deobfCompile ('io.sommers:packmode:1.12.1-1.2.0-SNAPSHOT.6')

    compile (project(":groovysandboxedlauncher"))/* {
        exclude group: 'org.codehaus.groovy'
    }*/
}

processResources {
    // this will ensure that this task is redone when the versions change.
    // inputs.property "version", project.version
    // inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    //if project name is BBM-Dev it makes the deobf jar deobf-dev if not its just deobf
    classifier = "deobf"
    //sets where the jars go to.
    mustRunAfter "jar"
}

shadowJar {
    classifier = ""
    // include "de.bloodworkxgaming.groovysandboxedlauncher.*"
    // include "org.kohsuke.groovy.sandbox.*"

    dependencies {
        include(project(':groovysandboxedlauncher'))
        //exclude(dependency('org.codehaus.groovy:.*'))

        //exclude("groovy/**")
        //exclude("groovyjarjarantlr/**")
        //exclude("groovyjarjarasm/**")
        //exclude("groovyjarjarcommonscli/**")
        //exclude("org/codehaus/**")


        // include "de/bloodworkxgaming//groovysandboxedlauncher//**"
        // include "org.kohsuke.groovy.sandbox.*"
    }
}

curseforge{
    if (project.hasProperty("thecursedkey")) {
        apiKey = project.thecursedkey
    }
    else {
        apiKey = ""
    }
    project {
        //CraftGroovy
        id = "284321"
        /*relations {
            requiredLibrary 'libvulpes'
        }*/
        changelog = file('changelog.html')
        changelogType = 'html'
        releaseType = "alpha"
        addGameVersion minecraft.version
        mainArtifact(shadowJar) {
            displayName = "CraftGroovy ${project.version}-UNSTABLE build " + project.getBuildNumber() +  " for " + minecraft.version
        }
        addArtifact (deobfJar){
            displayName = "CraftGroovy ${project.version}-UNSTABLE-deobf build " + project.getBuildNumber() + " for " + minecraft.version
        }
    }
}

task makeChangelog(type: GitChangelogTask) {
    file = new File ("changelog.html")
    untaggedName = "Current release ${project.version}"
    fromCommit = startGitRev
    toRef =  "HEAD"
    gitHubIssuePattern = "nonada123"
    templateContent = """
                        |{{#tags}}
                        |<h1> Highly untested and may break world</h1>
                        |  <h3>{{name}}</h3>
                        |  <ul>
                        |    {{#commits}}
                        |    <li> <a href="https://github.com/BloodWorkXGaming/CraftGroovy/commit/{{hash}}" target=_blank> {{{message}}}</a>
                        |</li>
                        |    {{/commits}}
                        |  </ul>
                        |{{/tags}}
                        |""".stripMargin()
}


reobf { shadowJar { mappingType = "SEARGE" } }
tasks.build.dependsOn makeChangelog, shadowJar

